import static org.gradle.api.JavaVersion.VERSION_17

plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = VERSION_17
targetCompatibility = VERSION_17

repositories {
	maven { url 'https://api.modrinth.com/maven'; content { includeGroup 'maven.modrinth' } }
	maven { url 'https://cursemaven.com'; content { includeGroup 'curse.maven' } }
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.gegy.dev/' }
	maven { url 'https://maven.shedaniel.me/' }
	maven { url 'https://maven.terraformersmc.com/' }
}

// Standard Dependencies
dependencies {
	// Cloth Config
	modImplementation "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}", {
		exclude group: 'net.fabricmc.fabric-api'
	}

	/*
	 * Fabric API.
	 *
	 * This is technically optional, but you probably want it anyway.
	 */
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

	// Fabric Loader
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"

	// Mappings - Official Mojang Mappings
	mappings loom.officialMojangMappings()

	// Minecraft
	minecraft "com.mojang:minecraft:${minecraft_version}"

	// Mod Menu
	modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
}

// Performant Dependencies
dependencies {
	// DynamicFPS
	modRuntimeOnly 'maven.modrinth:dynamic-fps:v2.1.0'

	// DataBreaker
	modRuntimeOnly 'supercoder79:databreaker:0.2.8', {
		exclude module: 'fabric-loader'
	}

	// FerriteCore
	modRuntimeOnly 'curse.maven:ferritecore-459857:3550048'
	
	// JOML
	runtimeOnly 'org.joml:joml:1.10.2'

	// Lithium
//	modRuntimeOnly 'curse.maven:lithium-360438:3659182'

	// Ksyxis
	modRuntimeOnly 'maven.modrinth:ksyxis:1.1'

	// Smooth Boot
	modRuntimeOnly 'curse.maven:smoothboot-415758:3549723'
	
	// Sodium
	modRuntimeOnly 'maven.modrinth:sodium:mc1.18.1-0.4.0-alpha6'

	// Starlight
	modRuntimeOnly 'curse.maven:starlight-521783:3554912'

	// TOML
	runtimeOnly 'com.moandjiezana.toml:toml4j:0.7.2'
}

processResources {
	inputs.property 'version', version

	filesMatching 'fabric.mod.json', {
		expand 'cloth_config_version': cloth_config_version,
		       'loader_version': loader_version,
		       'minecraft_version': minecraft_version,
		       'modmenu_version': modmenu_version,
		       'version': version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	/*
	 * Loom will automatically attach sourcesJar to a RemapSourcesJar task and
	 * to the "build" task if it is present.
	 *
	 * If you remove this line, sources will not be generated.
	 */
	withSourcesJar()
}

jar {
	from 'LICENSE', {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

/*
 * Maven publication configuration.
 */
publishing {
	publications {
		mavenJava MavenPublication, {
			from components.java
		}
	}

	/*
	 * See https://docs.gradle.org/current/userguide/publishing_maven.html for
	 *
	 * information on how to set up publishing.
	 */
	repositories {
		/*
		 * Add repositories to publish to here.
		 *
		 * Notice: This block does NOT have the same function as the block in
		 *         the top level.
		 *
		 * The repositories here will be used for publishing your artifact, not
		 * for retrieving dependencies.
		 */
	}
}
